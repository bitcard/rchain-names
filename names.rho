new
  basket,
  records,
  updateCh,
  createCh,
  changePriceCh,
  entry,
  validateAndDefaultsCh,
  entryUriCh,
  recordsUriCh,
  updateUriCh,
  priceNonceCh,
  publicKeyCh,
  recipientAddressCh,
  priceCh,
  registryLookup(`rho:registry:lookup`),
  secpVerify(`rho:crypto:secp256k1Verify`),
  insertArbitrary(`rho:registry:insertArbitrary`),
  stdout(`rho:io:stdout`),
  lookup(`rho:registry:lookup`),
  blake2b256(`rho:crypto:blake2b256Hash`),
  revAddress(`rho:rev:address`)
in {

  // 10 REV
  priceCh!(1000000000) |

  priceNonceCh!(NONCE) |
  publicKeyCh!(PUBLIC_KEY) |
  recipientAddressCh!(ADDRESS) |

  // Remove the following line (GENESIS....) if you don't want prereserved names
  //GENESIS_OPERATIONS
  records!({}) |

  // Remove the following block if you don't need
  // names from a previous deploy
  // START Records from previous deploy
  new oldRecordsCh, oldEntryUriCh in {
    registryLookup!(
      `rho:id:URI`,
      *oldEntryUriCh
    ) |
    for(oldEntry <- oldEntryUriCh) {
      oldEntry!((
        { "type": "READ" },
        *oldRecordsCh
      ))
    } |
    for (@oldRecords <- oldRecordsCh) {
      new iterate, recordUriCh, recordCh, n in {
        n!(0) |
        /* get old record and update it */
        for (@(name, uri) <= recordUriCh) {
          for (@val <- n) {
            n!(val + 1) |
            stdout!(val)
          } |
          registryLookup!(uri, *recordCh) |
          for(@r <- recordCh) {
            for (@current <- records) {
              new recordUriCh in {
                insertArbitrary!(
                  // Apply modifications to the record here
                  r.set("badges", {}),
                  *recordUriCh
                ) |
                for (@recordUri <- recordUriCh) {
                  records!(
                    current.set(
                      name,
                      recordUri
                    )
                  )
                }
              }
            }
          }
        } |
        for (@set <= iterate) {
          match set {
            Nil                 => {
              stdout!("End iteration")
            }
            Set(last)           => {
              // stdout!("last one") |
              recordUriCh!((last, oldRecords.get("records").get(last)))
            }
            Set(first ... rest) => {
              // stdout!(("first is", first, rest)) |
              iterate!(rest) |
              recordUriCh!((first, oldRecords.get("records").get(first)))
            }
            _ => {
              stdout!("End iteration")
            }
          }
        } |
        stdout!(oldRecords.get("records").keys()) |
        iterate!(oldRecords.get("records").keys())
      } |
      stdout!("found ${n} old records" %% { "n": oldRecords.get("records").size() })
    }
  } |
  // END Records from previous deploy


  // Change the price of record purchase
  contract changePriceCh(@payload, return) = {
    stdout!("changePriceCh") |
    new hashCh, verifySignatureChangePriceCh in {
      for (
        @priceNonce <- priceNonceCh;
        @publicKey <- publicKeyCh
      ) {
        publicKeyCh!(publicKey) |
        blake2b256!(
          priceNonce.toUtf8Bytes(),
          *hashCh
        ) |
        for (@hash <- hashCh) {
          secpVerify!(
            hash,
            payload.get("signature").hexToBytes(),
            publicKey.hexToBytes(),
            *verifySignatureChangePriceCh
          )
        } |
        for (@result <- verifySignatureChangePriceCh) {
          match result {
            true => {
              for (price <- priceCh) {
                return!(true) |
                priceCh!(payload.get("price")) |
                priceNonceCh!(payload.get("nonce"))
              }
            }
            false => {
              return!("error: Invalid signature, could not change price") |
              priceNonceCh!(priceNonce)
            }
          }
        }
      }
    }
  } |


  // Update a record
  contract updateCh(@record, return) = {
    new hashCh, verifySignatureCh in {
      for (current <- records) {
        match *current.contains(record.get("name")) {
          true => {
            match *current.get(record.get("name")) {
              registryUri => {
                new recordCh in {
                  lookup!(registryUri, *recordCh) |
                  for (currentRecord <- recordCh) {
                    blake2b256!(
                      *currentRecord.get("nonce").toUtf8Bytes(),
                      *hashCh
                    ) |
                    for (@hash <- hashCh) {
                      secpVerify!(
                        hash,
                        record.get("signature").hexToBytes(),
                        *currentRecord.get("publicKey").hexToBytes(),
                        *verifySignatureCh
                      ) |
                      for (@result <- verifySignatureCh) {
                        match result {
                          true => {
                            return!(true) |
                            new recordUriCh in {
                              insertArbitrary!(record, *recordUriCh) |
                              for (recordUri <- recordUriCh) {
                                records!(
                                  *current
                                    .delete(record.get("name"))
                                    .set(record.get("name"), *recordUri)
                                )
                              }
                            }
                          }
                          false => {
                            return!("error: signature invalid") |
                            records!(*current)
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          }
          false => {
            return!("error: name not found") |
            records!(*current)
          }
        }
      }
    }
  } |

  // create a record
  contract createCh(record, paymentDataCh) = {
    stdout!("createCh") |
    for (current <<- records) {
      match *current.contains(*record.get("name")) {
        true => {
          paymentDataCh!("error: name " ++ *record.get("name") ++ " already taken") |
          records!(*current)
        }
        false => {
          new revVaultCh,
            payRevReturnCh,
            payRevDataCh,
            revVaultCh,
            revAddressCh,
            paymentDoneCh,
            performCashingCh,
            RevAddress(`rho:rev:address`)
          in {
            for (@price <<- priceCh; @recipientAddress <<- recipientAddressCh) {
              registryLookup!(`rho:rchain:revVault`, *revVaultCh) |

              /*
                Create a vault/purse that is just used once (purse)
              */
              for(@(_, *revVault) <- revVaultCh) {
                new unf, purseRevAddrCh, purseAuthKeyCh, purseVaultCh, revAddressCh, paymentDoneCh in {
                  revAddress!("fromUnforgeable", *unf, *purseRevAddrCh) |
                  revVault!("unforgeableAuthKey", *unf, *purseAuthKeyCh) |
                  for (@purseAuthKey <- purseAuthKeyCh; @purseRevAddr <- purseRevAddrCh) {
                    stdout!({"new purse rev addr": purseRevAddr, "purse authKey": purseAuthKey}) |
                    revVault!("findOrCreate", purseRevAddr, *purseVaultCh) |
                    for (@(true, *vault) <- purseVaultCh) {
                      /*
                        Send payment data to client
                      */
                      paymentDataCh!(
                        (
                          {                         
                            "purseRevAddr": purseRevAddr,
                            "price": price,
                          },
                          *paymentDoneCh
                        )
                      ) |
                      for (@(resp, returnCh2) <- paymentDoneCh) {
                        new recipientVaultCh, vaultTo, revVaultkeyCh in {
                          match (purseRevAddr, recipientAddress, price) {
                            (revAddrFrom, revAddrTo, amount) => {
                              revVault!("findOrCreate", revAddrFrom, *recipientVaultCh) |
                              for (@vault <- recipientVaultCh) {
                                match vault {
                                  (true, vault) => {
                                    new resultCh in {
                                      @vault!("transfer", revAddrTo, amount, purseAuthKey, *resultCh) |
                                      for (@result <- resultCh) {
                                        stdout!(("Trying to transfer ", price, "from", revAddrFrom, "to", revAddrTo)) |
                                        stdout!(result) |
                                        match result {
                                          (true , Nil) => {
                                            new recordUriCh in {
                                              insertArbitrary!(*record, *recordUriCh) |
                                              for (r <- records; recordUri <- recordUriCh) {
                                                stdout!("success: record " ++ *record.get("name") ++ " created") |
                                                @returnCh2!(true) |
                                                records!(*current.set(*record.get("name"), *recordUri))
                                              }
                                            }
                                          }
                                          (false, err) => @returnCh2!(err)
                                        }
                                      }
                                    }
                                  }
                                  err => { payRevReturnCh!(err) }
                                }
                              }
                            }
                          }
                        }
                      }
                    }
                  }
                }
              }
            }
          }
        }
      }
    }
  } |

  contract validateAndDefaultsCh(@record, return) = {
    new x1, x2, x3, x4, s in {
      s!(record) |
      // Default values
      match record.get("servers") {
        Nil => {
          for (val <- s) {
            s!(*val.set("servers", [])) |
            x1!(true)
          }
        }
        _ => { x1!(true) }
      } |
      match record.get("badges") {
        Nil => {
          for (val <- s) {
            s!(*val.set("badges", {})) |
            x2!(true)
          }
        }
        _ => { x2!(true) }
      } |
      match record.get("address") {
        Nil => {
          for (val <- s) {
            s!(*val.set("address", Nil)) |
            x3!(true)
          }
        }
        _ => { x3!(true) }
      } |
      // just for UPDATE
      match record.get("signature") {
        Nil => {
          for (val <- s) {
            s!(*val.set("signature", Nil)) |
            x4!(true)
          }
        }
        _ => { x4!(true) }
      } |
      for (_ <- x1; _ <- x2; _ <- x3; _ <- x4) {
        for (val <- s) {
          match *val {
            {
              "publicKey": String,
              "nonce": String,
              "address": String \/ Nil,
              "signature": String \/ Nil, // just for UPDATE
              "name": String,
              "servers": _,
              "badges": _
            } => {
              return!((true, *val))
            }
            _ => {
              return!((false, "Invalid payload"))
            }
          }
        }
      }
    }
  } |

  contract entry(@(action, return)) = {
    stdout!(action) |
    match action.get("type") {
      "CREATE" => {
        new valueWithDefaultsCh in {
          validateAndDefaultsCh!(action.get("payload"), *valueWithDefaultsCh) |
          for (validateReturn <- valueWithDefaultsCh) {
            match *validateReturn {
              (true, recordWithDefaults) => {
                createCh!(recordWithDefaults, return)
              }
              (false, error) => {
                @return!(error)
              }
            }
          }
        }
      }
      "UPDATE" => {
        new valueWithDefaultsCh in {
          validateAndDefaultsCh!(action.get("payload"), *valueWithDefaultsCh) |
          for (validateReturn <- valueWithDefaultsCh) {
            match *validateReturn {
              (true, recordWithDefaults) => {
                updateCh!(recordWithDefaults, return)
              }
              (false, error) => {
                @return!(error)
              }
            }
          }
        }
      }
      "CHANGE_PRICE" => {
        match action.get("payload") {
          {
            "signature": String,
            "price": Int,
            "nonce": String,
          } => {
            changePriceCh!(action.get("payload"), return)
          }
          _ => {
            @return!("Invalid payload")
          }
        }
      }
      "READ" => {
        for (
          current <<- records;
          priceNonce <<- priceNonceCh;
          price <<- priceCh;
          publicKey <<- publicKeyCh;
          recipientAddress <<- recipientAddressCh
        ) {
          @return!({
            "records": *current,
            "priceNonce": *priceNonce,
            "price": *price,
            "publicKey": *publicKey,
            "recipientAddress": *recipientAddress
          })
        }
      }
    }
  } |

  insertArbitrary!(*entry, *entryUriCh) |

  for (entryUri <- entryUriCh) {
    stdout!({
      "registryUri": *entryUri,
      "recordsUnforgeableName": *records
    }) |
    basket!({
      "registryUri": *entryUri,
      "recordsUnforgeableName": *records
    })
  }
}
